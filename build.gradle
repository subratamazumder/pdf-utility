plugins {
    id 'java'
    id 'maven-publish'
}

group 'subrata-poc'
version '3.0-SNAPSHOT'
def pdfBoxVersion = '2.0.19'
sourceCompatibility = 1.8

configurations {
    // configuration that holds jars to include in the jar
    extraLibs
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
                'Created-By': 'Subrata Mazumder @ https://subratamazumder.github.io',
                'Implementation-Title': 'PDF Merge & Decrypt Utility utilising apache pdfbox '+ pdfBoxVersion,
                'Implementation-Version': archiveVersion,
                'Implementation-URL': 'https://github.com/subratamazumder/pdf-utility-java',
                'Main-Class': 'com.subrata.poc.pdf.Main'
        )
    }
}


repositories {
    mavenCentral()
}

dependencies {
    extraLibs group: 'org.apache.pdfbox', name: 'pdfbox', version: pdfBoxVersion
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.0'
    configurations.compile.extendsFrom(configurations.extraLibs)
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/subratamazumder/pdf-utility-java")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GH_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
